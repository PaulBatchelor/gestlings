#+TITLE: Parsing Programs
* Literate Programming, Weewiki, and Worgle
The Gestlings project makes heavy use of
=literate programming=, a programming style that attempts
to melt together structures
found in natural language and computer programs.

I've been using this paradigm quite a bit for myself,
and in truth, I'm still
not sure it's a great idea. But here I am, after all these
years, still compelled to try it.

Gestlings is a creative research endeavor that only
wishes to deeply think about particular ideas and concepts.
Literate
programming forces the author-programmer
to articulate what they are thinking. And what is research
but thinking deeply and articulating thoughts for others
to understand?

In past systems, such as the Loom, I've used Worgle,
my Tangler for literate programs, together with Weewiki,
my wiki engine, and have had decent success with the
combination. It is my hope to try
integrate these even more than previous attempts.

A big reason why Weewiki and Worgle work well together is
that they they both read org markup (or "orgdown", as some
would prefer to call it). A literate program written in
Worgle can become a wiki page in Weewiki with no
modifications. The problem is that pages can end up being
quite long, with no meaningful ways to navigate it.

The problem of navigation and "mapping" a program can be
solved using Worgle's metadata export feature.

A solution to the navigation or "mapping" problem in
presenting Worgle literate programs (a process known as 
"Weaving", the opposite of "tangling") may be found in
Worgle's metadata export feature.
This
metadata holds things like code block names and locations,
headers, and text. This can provide a great deal of information
about program structure in a language-agnostic way.

Here's the concept to explore: instead of linking worgle
programs directly to weewiki, export metadata, and then
have wiki pages dynamically reconstruct portions or all
of the literate program using =janet=. The =Loom= does
most of this via =worgmap=, except that in that case, the
pages are dynamically named and generated. In Gestlings, the
names page names would be persistent.
* Scope of Program Structure Parser
The goal of this document is to outline and implement
a set of usable operations that work on Worgle's
metadata output. These operations will built up on one
another, eventually making it possible to reconstruct
entire documents with embedded information useful for
navigating the document.

The scope of this functionality is only concerned with
producing weewiki output. Tangling code will not be
considered.
* Tangling
#+NAME: progparse.janet
#+BEGIN_SRC janet :tangle progparse.janet
<<progparse>>
<<codeblock-string>>
<<docgen>>
#+END_SRC
* Phase 1: Generating Documents
The first phase in parsing the metadata of a literate program
is to recreate the original org document. In theory,
all the components are there.
** Top Level Document Generator
The =docgen= program will produce a suitable weewiki
page for the id stored in =program=, using =title=
as the title for a page (worgle doesn't have title
export, yet).

The function iterates through the metadata (assuming
metadata exists for the program id), assembling the
entire document as one org string and then parsing it in
one go using the =org= function. Previously, this was
done using multiple calls to smaller parts of Org, but
this was causing things to crash (oops).

#+NAME: docgen
#+BEGIN_SRC janet
(defn docgen [program title]
  (var lines @[])
  (array/push lines (string "#+TITLE: " title "\n"))

  (def md (open-metadata "a.db" program))
  (def reslist (reslist md))
  (each res reslist
    (cond
      (is-header? (res "type"))
      (do
        (def header (get-header md (res "id")))
        (array/push
          lines
          (string
            (string/join
              (array/new-filled (header "level") "*"))
            " "
            (header "section") " " (header "name")"\n")))

      (is-content? (res "type"))
      (do
        (def content (get-content md (res "id")))
        (array/push lines (content "content")))
      (is-blockref? (res "type"))
      (do
        (def blockref
          (get-blockref md (res "id")))
        (def block
          (get-block md (blockref "ref")))
        (def code (codeblock-string md (res "id")))
        (array/push lines code))))

  (def orgstr (string/join lines ""))
  (def fp (file/open "output.org" :w))
  (file/write fp orgstr)
  (file/close fp)
  (org orgstr))
#+END_SRC
** Opening/Closing the Metadata
#+NAME: progparse
#+BEGIN_SRC janet
(defn open-metadata [name program]
  (table
    :db (sqlite3/open name)
    :program program))
#+END_SRC

#+NAME: progparse
#+BEGIN_SRC janet
(defn close-metadata [md] (sqlite3/close (md :db)))
#+END_SRC
** Getting the Resource List
Text in worgle document is broken up in to components
known as =resources=. The inventory of these resources
is placed in the =resource list=.

The janet function =reslist= returns this list given
a valid program id.

#+NAME: progparse
#+BEGIN_SRC janet
(defn reslist [md]
  (sqlite3/eval
    (md :db)
    (string/format
      (string
        "SELECT id, type FROM resources WHERE program=%d "
        "ORDER by id ASC")
      (md :program))))
#+END_SRC
** Type Checking
Hard coded in a lookup table here, but it is feasible
(though highly unlikely) for these values to change. See
the schema for `reslist` for a key.

#+NAME: progparse
#+BEGIN_SRC janet
(defn is-file? [id] (= id 0))
(defn is-block? [id] (= id 1))
(defn is-segment? [id] (= id 2))
(defn is-header? [id] (= id 3))
(defn is-content? [id] (= id 4))
(defn is-blockref? [id] (= id 5))
#+END_SRC
** Section Headers
*** Getting Header Data
#+NAME: progparse
#+BEGIN_SRC janet
(defn get-header [md id]
  ((sqlite3/eval
     (md :db)
     (string/format
       "SELECT * FROM headers WHERE id=%d AND program=%d"
       id (md :program))) 0))
#+END_SRC
*** Printing Header Data
** Content
Content refers to text in between section headers.
*** Getting Content Data
#+NAME: progparse
#+BEGIN_SRC janet
(defn get-content [md id]
  ((sqlite3/eval
     (md :db)
     (string/format
       "SELECT * FROM content WHERE id=%d AND program=%d"
       id (md :program))) 0))
#+END_SRC
*** Printing Content Data
** Block References
*** Getting Block Reference Data
#+NAME: progparse
#+BEGIN_SRC janet
(defn get-blockref [md id]
  (def q (sqlite3/eval
     (md :db)
     (string/format
       "SELECT * FROM blkref WHERE id=%d AND program=%d"
       id (md :program))))

  (if (> (length q) 0) (q 0) nil))
#+END_SRC
*** Printing Block Reference Data
** Code Blocks
*** Getting Code Blocks
#+NAME: progparse
#+BEGIN_SRC janet
(defn get-block [md id]
  ((sqlite3/eval
     (md :db)
     (string/format
       "SELECT * FROM blocks WHERE id=%d AND program=%d"
       id (md :program))) 0))
#+END_SRC
*** Printing Code Blocks
The =codeblock-string= function creates an org-compatible
codeblock from a block id and reference to an open metadata
handle.

Regenerating a particular codeblock from Worgle's Metadata
can be considered a non-trivial task. What makes codeblock
regeneration complicated is that a named codeblock can
actually have more than one block, called a =subblock=. A
block reference will contain information for both the
block and subblock. Due to the way Worgle populates metdata,
some values will have to be computed from the data.

See the comments in the code below for some clarification
on the procedure.

Much more information about codeblock regeneration can be
found in the Worgmap core operations code.

#+NAME: codeblock-string
#+BEGIN_SRC janet
(defn codeblock-string [md id]
  # get block reference data
  (def br (get-blockref md id))

  # Find next block reference, if there is one
  (def next-br-query
    (sqlite3/eval
      (md :db)
      (string
        "SELECT * FROM blkref WHERE "
        "program == " (md :program) " AND "
        "ref == " (br "ref") " AND "
        "pos == " (+ (br "pos") 1))))

  (defn handle-seg [s]
    (cond
      (= (s "type") 1) (string "<<" (s "str") ">>")
      (= (s "type") 0) (s "str")))

  # Get code block being referenced
  (def block (get-block md (br "ref")))

  # Compute number of segments in subblock.
  # If there's another block reference, subtract their
  # segment offsets. Otherwise, subtract segment offset
  # from total.

  (def nsegs
    (if (> (length next-br-query) 0)
      (- ((next-br-query 0) "segoff") (br "segoff"))
      (- (block "nsegs") (br "segoff"))))

  (def lines (array/new nsegs))
  (var next-seg nil)

  # Finding the head segment of the subblock requires
  # looking at the last segment of the previous subblock
  # that last segment will point to the head of the current
  # block. If there is no previous subblock, it's just the
  # head segment stored in the block struct.

  (if (> (br "prev_lastseg") 0)
    (do
      (def prevseg (get-segment md (br "prev_lastseg")))
      (def head (get-segment  md (prevseg "next_segment")))
      (set next-seg head)
      (array/push lines (handle-seg head)))
    (do
      (def head (get-segment md (block "head_segment")))
      (set next-seg head)
      (array/push
        lines
        (handle-seg head))))

  (for i 1 nsegs
    (def seg (get-segment md (next-seg "next_segment")))
    (array/push lines (handle-seg seg))
    (set next-seg seg))

  (if-not (= (string/slice (array/peek lines) -2) "\n")
    (array/push lines "\n"))

  (string
    "#+NAME: " (block "name") "\n"
    "#+BEGIN_SRC\n"
    (string/join lines "\n")
    "#+END_SRC\n"))
#+END_SRC
** Segments
#+NAME: progparse
#+BEGIN_SRC janet
(defn get-segment [md id]
  ((sqlite3/eval
     (md :db)
     (string/format
       "SELECT * FROM segments WHERE id=%d AND program=%d"
       id (md :program))) 0))
#+END_SRC
** Tangled Files
*** Getting File Data
* Phase 2: Map Generation
Once it can be shown that a document of a literate program
can be produced from its metadata, the next step is to
augment it with better navigation.
